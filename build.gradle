plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
    id "maven-publish"
}

group = 'com.anaplan.engineering'
version = '3.0.2'

sourceSets {
    example {
        kotlin {
            srcDir { 'src/examples/kotlin' }
        }
        java {
            srcDir { 'src/examples/java' }
        }
        resources {
            srcDir { 'src/examples/resources' }
        }
        compileClasspath += sourceSets.main.compileClasspath + sourceSets.main.output
        runtimeClasspath += compileClasspath + output
    }
}

ext {
    kotlinVersion = "1.5.31"
    overtureVersion = project.version
}

repositories {
    mavenCentral()
}

allprojects {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion

    implementation group: 'org.overturetool.core', name: 'interpreter', version: overtureVersion

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito.kotlin', name: 'mockito-kotlin', version: '4.0.0'
}

// Using artifactory plugin appears broken -- https://github.com/gradle/gradle/issues/4783
publishing {
    repositories {
        maven {
            credentials {
                username System.env.ART_USERNAME
                password System.env.ART_PASSWORD
            }
            url findProperty('ART_URL')
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
